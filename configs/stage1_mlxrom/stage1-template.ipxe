#!ipxe

# Global settings.
# TODO: set epoxyaddress boot-api-dot-{{project}}.appspot.com
set epoxyaddress epoxy-boot-api.{{project}}.measurementlab.net

set menu_timeout_ms:int32 5000
set fetch_timeout_ms 10000

set retry_delay_s:int32 30
set max_retry_delay_s 480

# IPv6 network configuration.
# ipv6_enabled will always be defined. When ipv6_enabled is "false", then other
# ipv6 variables are undefined.
set ipv6_enabled {{ipv6_enabled}}
set ipv6_address {{ipv6_address}}
set ipv6_gateway {{ipv6_gateway}}
set ipv6_subnet  64
set ipv6_dns1    {{ipv6_dns1}}
set ipv6_dns2    {{ipv6_dns2}}

# IPv4 network configuration.
set ipv4_address {{ip}}
set ipv4_gateway {{gateway}}
set ipv4_subnet  26
set ipv4_netmask {{netmask}}
set ipv4_dns1    {{dns1}}
set ipv4_dns2    {{dns2}}

# Apply ipxe network settings (IPv4 only).
ifopen net0
set net0/ip ${ipv4_address}
set net0/gateway ${ipv4_gateway}
set net0/netmask ${ipv4_netmask}
set net0/dns ${ipv4_dns1}
set hostname {{hostname}}

# Save the GCP project name.
set project {{project}}

# The ePoxy stage1 URL.
set stage1_url https://${epoxyaddress}/v1/boot/${hostname}/stage1.ipxe

########################################
# Boot menu.
:startmenu
  # Double menu delay for every retry.
  inc menu_timeout_ms ${menu_timeout_ms}
  menu M-Lab iPXE boot menu: ${epoxyaddress}
    item --gap --   Production options:
    item stage1     -- Boot stage1 script
    item localboot  -- Boot local disk
    item --gap
    item --gap --   Diagnostic options:
    item shell      -- iPXE Shell
    item configure  -- Configure settings
    item reboot     -- Reboot
  choose --timeout ${menu_timeout_ms} boottype

  # Jump to the corresponding menu option.
  goto ${boottype}


########################################
# Diagnostic options.
:shell
  echo Type 'exit' to get back to the menu.
  shell
  goto startmenu

:configure
  echo Starting config.
  config
  goto startmenu

:reboot
  reboot


########################################
# Production options.
:stage1
  echo Fetching stage1 script.
  goto firstfetch

:retry_loop iseq ${retry_delay_s} ${max_retry_delay_s} && goto fetch_timeout_local_boot ||
  inc retry_delay_s ${retry_delay_s}
  echo Sleeping ${retry_delay_s} seconds...
  sleep ${retry_delay_s}

:firstfetch
  # Setup parameters to send to server.
  params
  param platform     ${platform}      # pcbios / efi.
  param buildarch    ${buildarch}     # i386 / x86_64
  param serial       ${serial}        # system serial number.
  param asset        ${asset}         # system asset tag.
  param uuid         ${uuid}          # system uuid.
  param manufacturer ${manufacturer}  # system manufacturer.
  param product      ${product}       # system product.
  param chip         ${chip}          # NIC chipset.
  param mac          ${mac}           # NIC MAC address.
  param ip           ${ip}            # IP address.
  param version      ${version}       # iPXE version.

  # Issues an HTTP POST request.
  imgfetch --name stage1.ipxe --timeout ${fetch_timeout_ms} ${stage1_url}##params || goto retry_loop
:loop_done


:verify_image
  echo Verifying and booting stage1 script.
  chain     stage1.ipxe              || goto verify_error


:verify_error
  echo Failed to chain load stage1 script. Sleeping 5 seconds and starting over.
  sleep 5
  goto startmenu


:localboot
:fetch_timeout_local_boot
  echo Sleeping 10 seconds and booting from local hard drive.
  sleep 10
  sanboot --no-describe --drive 0x80 || shell
